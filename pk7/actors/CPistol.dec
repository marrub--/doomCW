actor CPistol : CWeapon replaces Pistol
{
	Weapon.SlotNumber 2
	Weapon.AmmoType1 "Clip"
	Obituary "$CW_PISTOL_OBIT"
	Tag "$CW_PISTOL_WTAG"
	States
	{
	// Spawn //
	Spawn:
		PIST A -1
		wait
	// Select //
	Select:
		PISG A 0 A_GiveInventory("DNS")
		PISG A 0 A_JumpIfInventory("PistolSil",1,1)
		goto SelectLoop
	SilSelect:
		PISS A 0
	SelectLoop:
		"####" A 1 A_Raise
		wait
	
	Deselect:
		PISG A 0 A_GiveInventory("DNS")
		PISG A 0 A_JumpIfInventory("PistolSil",1,1)
		goto DeselectLoop
	SilDeselect:
		PISS A 0
	DeselectLoop:
		"####" A 1 A_Lower
		wait
	
	// Ready //
	SilReady:
		PISS A 1 A_WeaponReady
	Ready:
		TNT1 A 0 A_TakeInventory("DNS")
		TNT1 A 0 A_JumpIfInventory("PistolSil",1,"SilReady")
		PISG A 1 A_WeaponReady
		loop
	
	// Fire //
	Fire:
		TNT1 A 0 A_JumpIfInventory("PistolSil",1,"SilFire")
		PISG A 9 A_SetTics(CallACS("CW_GETVAL",GV_PSTF1))
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("Flash2")
		TNT1 A 0 A_PlaySound("weapons/pistol",1)
		PISG A 4 BRIGHT A_FireBullets(CallACS("CW_GETVAL",GV_PSTF2),0,1,5,"BulletPuff")
		PISG DC 3
		PISG A 5 A_Refire
		goto Ready
	SilFire:
		PISS A 9 A_SetTics(CallACS("CW_GETVAL",GV_PSTF1))
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/silpistol",1)
		PISS A 4 A_FireBullets(CallACS("CW_GETVAL",GV_PSTF2),0,1,4,"BulletPuff")
		PISS DC 3
		PISS A 5 A_Refire
		goto Ready
	
	Flash:
		TNT1 A 0 A_Quake(1, 2, 0, 16, "")
		TNT1 A 1 A_SetPitch(pitch-0.2)
		TNT1 A 1 A_SetPitch(pitch-0.2)
		TNT1 A 1 A_SetPitch(pitch-0.1)
		TNT1 A 2 A_SetPitch(pitch+0.4)
		stop
	Flash2:
		TNT1 A 0 A_Quake(1, 2, 0, 16, "")
		TNT1 A 0 A_SetBlend("99 00 00", 0.02, 4)
		TNT1 A 0 A_SetPitch(pitch-0.1)
		PISF Z 1 BRIGHT A_Light2
		PISF Z 1 BRIGHT A_SetPitch(pitch-0.2)
		TNT1 A 0 A_SetPitch(pitch+0.4)
		PISF Z 2 BRIGHT A_Light1
		goto LightDone
	}
}

actor CPistolToSil : Weapon
{
	+WEAPON.NOALERT
	+WEAPON.CHEATNOTWEAPON
	Tag "$CW_PISTOL_WTAG"
	States
	{
	Select:
		TNT1 A 0 A_GiveInventory("DNS")
		TNT1 AAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0
		stop
	Ready:
		TNT1 A 5 A_PlaySound("weapons/pistolupg",1)
		TNT1 A 20
		TNT1 A 0 A_GiveInventory("PistolSil")
		TNT1 A 0 A_SelectWeapon("CPistol")
		TNT1 A 0 A_TakeInventory("CPistolToSil")
		stop
	Fire:
		TNT1 A 0 A_Jump(257,"Ready")
		goto Ready
	}
}

actor CPistolToNorm : CPistolToSil
{
	States
	{
	Ready:
		TNT1 A 0 A_TakeInventory("PistolSil")
		TNT1 A 5 A_PlaySound("weapons/pistolupg",1)
		TNT1 A 10
		TNT1 A 0 A_SelectWeapon("CPistol")
		TNT1 A 0 A_TakeInventory("CPistolToNorm")
		stop
	}
}

actor CPistolCMenu : Weapon
{
	+WEAPON.NOALERT
	+WEAPON.CHEATNOTWEAPON
	Tag "$CW_PISTOL_WTAG"
	States
	{
	// Select //
	Select:
		TNT1 A 0 A_GiveInventory("DNS")
		TNT1 A 0 A_JumpIfInventory("PistolSil",1,1)
		goto SelectLoop
	SilSelect:
		TNT1 B 0
	SelectLoop:
		TNT1 "#" 0 A_Raise
		PISC "#" 1 A_Raise
		loop
	
	Deselect:
		TNT1 A 0 A_GiveInventory("DNS")
		TNT1 A 0 A_JumpIfInventory("PistolSil",1,1)
		goto DeselectLoop
	SilDeselect:
		TNT1 B 0
	DeselectLoop:
		TNT1 "#" 0 A_Lower
		PISC "#" 1 A_Lower
		loop
	
	// Ready //
	Ready:
		TNT1 "#" 0 A_GiveInventory("CMenu")
		TNT1 "#" 0 ACS_NamedExecute("CW_MENUOPEN",0,1)
		PISC "#" 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOSWITCH)
		wait
	Zoom:
		TNT1 "#" 0 A_TakeInventory("CMenu")
		TNT1 "#" 0 ACS_NamedExecute("CW_MENUCLOSE",0)
		TNT1 "#" 0 A_Lower
		PISC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		PISC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		PISC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		PISC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		PISC "#" 1 A_Lower
		TNT1 "#" 0 A_SelectWeapon("CPistol")
		TNT1 "#" 0 A_TakeInventory("CPistolCMenu")
		stop
	
	// Fire //
	Fire:
		TNT1 "#" 0 A_JumpIfInventory("CMenuNext",1,"Fire2")
		TNT1 "#" 0 A_GiveInventory("CMenuNext")
		PISC "#" 13 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		goto Ready
	Fire2:
		TNT1 "#" 0 A_TakeInventory("CMenuNext")
		PISC "#" 13 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		goto Ready
	}
}

