actor CShotgun : CWeapon replaces Shotgun
{
	Weapon.SlotNumber 3
	Weapon.AmmoType1 "Shell"
	Obituary "$CW_SHOTGN_OBIT"
	Tag "$CW_SHOTGN_WTAG"
	States
	{
	Spawn:
		SHOT A 1
		loop
	
	// Select //
	Select:
		SHTG A 0 A_GiveInventory("DNS")
		SHTG A 0 A_JumpIfInventory("ShotgunSil",1,1)
		goto SelectLoop
	SilSelect:
		SHTS A 0
	SelectLoop:
		"####" A 1 A_Raise
		wait
	
	Deselect:
		SHTG A 0 A_GiveInventory("DNS")
		SHTG A 0 A_JumpIfInventory("ShotgunSil",1,1)
		goto DeselectLoop
	SilDeselect:
		SHTS A 0
	DeselectLoop:
		"####" A 1 A_Lower
		wait
	
	// Ready //
	SilReady:
		SHTS A 1 A_WeaponReady
	Ready:
		TNT1 A 0 A_TakeInventory("DNS")
		TNT1 A 0 A_JumpIfInventory("ShotgunSil",1,"SilReady")
		SHTG A 1 A_WeaponReady
		loop
	
	// Fire //
	Fire:
		TNT1 A 0 A_JumpIfInventory("ShotgunSil",1,"SilFire")
		SHTG A 9 A_SetTics(CallACS("CW_GETVAL",GV_PSTF1))
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		SHTG A 7 BRIGHT A_FireBullets(7,0,12,2,"BulletPuff")
		SHTG B 5
		SHTG C 5 A_PlaySound("weapons/shogupg",5)
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_Refire
		goto Ready
	SilFire:
		SHTS A 9 A_SetTics(CallACS("CW_GETVAL",GV_PSTF1))
		SHTS A 2 A_PlaySound("weapons/silpistol",1)
		SHTS A 5 BRIGHT A_FireBullets(7,0,12,2,"BulletPuff")
		SHTS B 5
		SHTS C 5 A_PlaySound("weapons/shogupg",5)
		SHTS D 4
		SHTS CB 5
		SHTS A 3
		SHTS A 7 A_Refire
		goto Ready
	
	Flash:
		SHTF A 1 A_PlaySound("weapons/shotty",1)
		SHTF A 1 A_Light2
		SHTF B 2 A_Light1
		goto LightDone
	}
}

actor CShotgunToSil : Weapon
{
	+WEAPON.NOALERT
	+WEAPON.CHEATNOTWEAPON
	Tag "$CW_SHOTGN_WTAG"
	States
	{
	Select:
		TNT1 A 0 A_GiveInventory("DNS")
		TNT1 AAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0
		stop
	Ready:
		TNT1 A 5 A_PlaySound("weapons/shogupg",1)
		TNT1 A 20
		TNT1 A 0 A_GiveInventory("ShotgunSil")
		TNT1 A 0 A_SelectWeapon("CShotgun")
		TNT1 A 0 A_TakeInventory("CShotgunToSil")
		stop
	Fire:
		TNT1 A 0 A_Jump(257,"Ready")
		goto Ready
	}
}

actor CShotgunToNorm : CShotgunToSil
{
	States
	{
	Ready:
		TNT1 A 0 A_TakeInventory("ShotgunSil")
		TNT1 A 5 A_PlaySound("weapons/shogupg",1)
		TNT1 A 10
		TNT1 A 0 A_SelectWeapon("CShotgun")
		TNT1 A 0 A_TakeInventory("CShotgunToNorm")
		stop
	}
}

actor CShotgunCMenu : Weapon
{
	+WEAPON.NOALERT
	+WEAPON.CHEATNOTWEAPON
	Tag "$CW_SHOTGN_WTAG"
	States
	{
	// Select //
	Select:
		TNT1 A 0 A_GiveInventory("DNS")
		TNT1 A 0 A_JumpIfInventory("ShotgunSil",1,1)
		goto SelectLoop
	SilSelect:
		TNT1 B 0
	SelectLoop:
		TNT1 "#" 0 A_Raise
		SHTC "#" 1 A_Raise
		loop
	
	Deselect:
		TNT1 A 0 A_GiveInventory("DNS")
		TNT1 A 0 A_JumpIfInventory("ShotgunSil",1,1)
		goto DeselectLoop
	SilDeselect:
		TNT1 B 0
	DeselectLoop:
		TNT1 "#" 0 A_Lower
		SHTC "#" 1 A_Lower
		loop
	
	// Ready //
	Ready:
		TNT1 "#" 0 A_GiveInventory("CMenu")
		TNT1 "#" 0 ACS_NamedExecute("CW_MENUOPEN",0)
		SHTC "#" 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOSWITCH)
		wait
	Zoom:
		TNT1 "#" 0 A_TakeInventory("CMenu")
		TNT1 "#" 0 ACS_NamedExecute("CW_MENUCLOSE",0)
		TNT1 "#" 0 A_Lower
		SHTC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		SHTC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		SHTC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		SHTC "#" 1 A_Lower
		TNT1 "#" 0 A_Lower
		SHTC "#" 1 A_Lower
		TNT1 "#" 0 A_SelectWeapon("CShotgun")
		TNT1 "#" 0 A_TakeInventory("CShotgunCMenu")
		stop
	
	// Fire //
	Fire:
		TNT1 "#" 0 A_JumpIfInventory("CMenuNext",1,"Fire2")
		TNT1 "#" 0 A_GiveInventory("CMenuNext")
		SHTC "#" 13 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		goto Ready
	Fire2:
		TNT1 "#" 0 A_TakeInventory("CMenuNext")
		SHTC "#" 13 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		goto Ready
	}
}

