actor CShotgun : CWeapon replaces Shotgun
{
	Weapon.SlotNumber 3
	Weapon.AmmoType1 "Shell"
	AttackSound "weapons/shotty"
	Obituary "$CW_SHTGUN_OBIT"
	Tag "$CW_SHTGUN_WTAG"
	States
	{
	// Select //
	Select:
		TNT1 A 0 A_GiveInventory("DNS",1)
		SHTG A 1 A_Raise
		wait
	Deselect:
		TNT1 A 0 A_GiveInventory("DNS",1)
		SHTG A 1 A_Lower
		wait
	
	// Ready //
	Ready:
		TNT1 A 0 A_TakeInventory("DNS",1)
		SHTG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		loop
	
	// Fire //
	Fire:
		SHTG A 9 A_SetTics(CallACS("CW_GETVAL",GV_PSTF1))
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		SHTG A 5 BRIGHT A_FireBullets(7,0,12,2,"BulletPuff")
		SHTG B 5
		SHTG C 5 A_PlaySound("weapons/shogupg",5)
		SHTG DCB 5
		SHTG A 6 A_Refire
		goto Ready
	
	Flash:
		SHTF A 2 A_Light2
		SHTF B 2 A_Light1
		goto LightDone
	}
}

actor CShotgunSil : CWeapon
{
	+WEAPON.CHEATNOTWEAPON
	Weapon.SlotNumber 3
	Weapon.AmmoType1 "Shell"
	AttackSound "weapons/silpistol"
	Obituary "$CW_SSHTGN_OBIT"
	Tag "$CW_SSHTGN_WTAG"
	States
	{
	// Select //
	Select:
		TNT1 A 0 A_GiveInventory("DNS",1)
		SHTS A 1 A_Raise
		wait
	Deselect:
		TNT1 A 0 A_GiveInventory("DNS",1)
		SHTS A 1 A_Lower
		wait
	
	// Ready //
	Ready:
		TNT1 A 0 A_TakeInventory("DNS",1)
		SHTS A 1 A_WeaponReady(WRF_ALLOWZOOM)
		loop
	
	// Fire //
	Fire:
		SHTS A 9 A_SetTics(CallACS("CW_GETVAL",GV_PSTF1))
		SHTS A 5 BRIGHT A_FireBullets(7,0,12,2,"BulletPuff")
		SHTS B 5
		SHTS C 5 A_PlaySound("weapons/shogupg",5)
		SHTS DCB 5
		SHTS A 6 A_Refire
		goto Ready
	}
}

actor CShotgunToSil : Weapon
{
	+WEAPON.CHEATNOTWEAPON
	Tag "$CW_PISTOL_WTAG"
	States
	{
	Select:
		TNT1 A 0 A_GiveInventory("DNS",1)
		TNT1 AAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0
		stop
	Ready:
		TNT1 A 0 A_TakeInventory("CShotgun")
		TNT1 A 5 A_PlaySound("weapons/shogupg",1)
		TNT1 A 20
		TNT1 A 0 A_GiveInventory("CShotgunSil")
		TNT1 A 0 A_SelectWeapon("CShotgunSil")
		TNT1 A 0 A_TakeInventory("CShotgunToSil")
		stop
	Fire:
		TNT1 A 0 A_Jump(257,"Ready")
		goto Ready
	}
}

actor CShotgunToNorm : CShotgunToSil
{
	Tag "$CW_SPISTL_WTAG"
	States
	{
	Ready:
		TNT1 A 0 A_TakeInventory("CShotgunSil")
		TNT1 A 5 A_PlaySound("weapons/shogupg",1)
		TNT1 A 10
		TNT1 A 0 A_GiveInventory("CShotgun")
		TNT1 A 0 A_SelectWeapon("CShotgun")
		TNT1 A 0 A_TakeInventory("CShotgunToNorm")
		stop
	}
}